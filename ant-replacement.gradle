buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name "gradle plugins"
            url "http://dl.bintray.com/gradle/gradle-plugins"
        }
    }
    dependencies {
        classpath 'com.squareup.okhttp3:okhttp:3.0.1'
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.4'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}
apply plugin: org.hidetake.gradle.ssh.plugin.SshPlugin
apply plugin: com.github.jengelman.gradle.plugins.shadow.ShadowPlugin

// === Build properties === //
ext {
    currentVersion = 'current'
    wpilib = System.getProperty('user.home') + "/wpilib/java/${currentVersion}"
    wpilibAntDir = "${wpilib}/ant"
    wpiUser = 'lvuser'
    wpiUserHome = "/home/${wpiUser}"
    robotPackage = 'org.usfirst.frc.team5818.robot'
    robotClass = "${robotPackage}.Robot"
}
// === End B properties === //

jar {
    manifest {
        attributes(
            "Main-Class": 'edu.wpi.first.wpilibj.RobotBase',
            "Robot-Class": project.robotClass,
            "Class-Path": '.'
        )
    }
}

// isReachable test
boolean isReachable(String addr, int openPort, int timeOutMillis) {
    try {
        new Socket().withCloseable { soc ->
            soc.connect(new InetSocketAddress(addr, openPort), timeOutMillis);
        }
        return true;
    } catch (IOException ex) {
        return false;
    }
}

boolean canReachSSH(String addr) {
    return isReachable(addr, 22, 100);
}

if (!project.hasProperty('teamNumber')) {
    println "No team number provided."
    throw new AssertionError("No team number.");
}

String getStaticIp() {
    if (project.hasProperty("staticIP")) {
        return project.staticIP
    }
    int top = project.teamNumber / 100
    int lower = project.teamNumber % 100
    return "10.${top}.${lower}.30"
}

task initializeRobotIP() {
    description 'Discovers the robot\'s IP or fails'
} << {
    println "Press enter when ready to upload."
    new Scanner(System.in).withCloseable({ s -> s.nextLine() });
    String robotIP = "roboRIO-${project.teamNumber}-FRC.local";
    if (!canReachSSH(robotIP)) {
        println "Couldn't reach $robotIP, trying USB"
        robotIP = "172.22.11.2"
        if (!canReachSSH(robotIP)) {
            println "Couldn't reach $robotIP, trying static IP"
            robotIP = getStaticIp();
            if (!canReachSSH(robotIP)) {
                println "Couldn't reach the robot via $robotIP, aborting!"
                throw new AssertionError("Couldn't reach the robot");
            } else {
                println "Found robot via static IP"
            }
        } else {
            println "Found robot via USV"
        }
    } else {
        println "Found robot via mDNS"
    }
    project.ext.robotIP = robotIP;
    project.remotes.create('robot') {
        host = robotIP
        user = project.wpiUser
        password = ''
        knownHosts = allowAnyHosts
    }
}
// It's best to compile first.
initializeRobotIP.shouldRunAfter jar
initializeRobotIP.shouldRunAfter shadowJar

import okhttp3.*;
task checkSysProps(dependsOn: [initializeRobotIP]) {
    description 'Checks the roboRIO image version.'
} << {
    def XML = MediaType.parse("text/xml; charset=utf-16le");
    def client = new OkHttpClient();
    def body = new FormBody.Builder()
                .add('Function', 'GetPropertiesOfItem')
                .add('Plugins', 'nisyscfg')
                .add('Items', 'system')
                .build();
    Request request = new Request.Builder()
        .url("http://${project.robotIP}/nisysapi/server")
        .post(body)
        .build();
    Response response = client.newCall(request).execute();
    def hexemell = response.body().string();
    def imageMatch = hexemell =~ /FRC_roboRIO_2016_v([0-9]+)/
    if (!imageMatch) {
        throw new IllegalStateException("Image version not found.");
    }
    def allowedImage = '19'
    def image = imageMatch.group(1)
    if (!allowedImage.contains(image)) {
        throw new IllegalStateException("roboRIO Image does not match plugin, allowed image version: ${allowedImage}");
    }
    println 'roboRIO image version validated'
}

task checkJRE(dependsOn: [initializeRobotIP]) {
    description 'Checks the JRE of the roboRIO'
} << {
    ssh.run {
        session(remotes.robot) {
            execute 'test -d /usr/local/frc/JRE'
        }
    }
    println 'Found JRE.'
}

task checkRoboRIO(dependsOn: [checkSysProps, checkJRE]) {
    // Just a splitter for the sub-tasks.
    description 'Checks that the roboRIO is what we expect.'
}

void defineDeployTask(Object project, boolean debug) {
    project.tasks.create(name: 'deploy' + (debug ? 'Debug' : ''),
                         dependsOn: [shadowJar, checkRoboRIO]) {
        description "Deploys the jar to the robot. ${debug ? 'Waits' : 'Does not wait'} for a debugger."
        ext.jarFile = project.shadowJar.archivePath
        ext.commandFile = "${project.wpilibAntDir}/robot${debug ? 'Debug' : ''}Command"
        inputs.file jarFile
        inputs.file commandFile
        if (debug) {
            ext.frcDebugFile = "${project.wpilibAntDir}/frcdebug"
            inputs.file frcDebugFile
        }
    } << {
        ssh.run {
            session(remotes.robot) {
                println "Copying jar"
                put from: jarFile, into: "${project.wpiUserHome}/FRCUserProgram.jar"
                execute 'killall -q netconsole-host', ignoreError: true
                put from: commandFile, into: project.wpiUserHome
                if (debug) {
                    println "Setting debug flag"
                    put from: frcDebugFile, into: '/tmp/frcdebug'
                    execute 'chown lvuser:ni /tmp/frcdebug'
                }
                println "Starting program"
                execute '. /etc/profile.d/natinst-path.sh; /usr/local/frc/bin/frcKillRobot.sh -t -r', ignoreError: true
                if (!debug) {
                    execute 'sync'
                }
            }
        }
    }
}
defineDeployTask(project, false)
defineDeployTask(project, true)
